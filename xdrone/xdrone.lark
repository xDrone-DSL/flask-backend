main: "main" "(" ")" "{" takeoff command* land "}"

takeoff: "takeoff" "(" ")"
land:    "land"    "(" ")"

?command: up
       | down
       | left
       | right
       | forward
       | backward
       | rotatel
       | rotater
       | wait
       | action
       | repeat

up: "up" "(" distance ")"
down: "down" "(" distance ")"
left: "left" "(" distance ")"
right: "right" "(" distance ")"
forward: "forward" "(" distance ")"
backward: "backward" "(" distance ")"
rotatel: "rotate_left" "(" angle ")"
rotater: "rotate_right" "(" angle ")"
wait: "wait" "(" seconds ")"
action: "action" "(" ")"

repeat: "repeat" number "times" "{" command* "}"

number : DECIMAL
        | INT

?distance : number
?seconds  : number
?angle    : number

%import common.DECIMAL
%import common.INT

%import common.WS
%ignore WS
